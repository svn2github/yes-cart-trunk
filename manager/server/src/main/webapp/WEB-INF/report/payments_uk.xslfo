<?xml version="1.0" encoding="utf-8"?>
<!--
  ~ Copyright 2009 Igor Azarnyi, Denys Pavlov
  ~
  ~    Licensed under the Apache License, Version 2.0 (the "License");
  ~    you may not use this file except in compliance with the License.
  ~    You may obtain a copy of the License at
  ~
  ~        http://www.apache.org/licenses/LICENSE-2.0
  ~
  ~    Unless required by applicable law or agreed to in writing, software
  ~    distributed under the License is distributed on an "AS IS" BASIS,
  ~    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  ~    See the License for the specific language governing permissions and
  ~    limitations under the License.
  -->

<xsl:stylesheet version="2.0"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:fo="http://www.w3.org/1999/XSL/Format">
    <xsl:output method="xml" encoding="UTF-8" indent="yes"/>

    <xsl:key name="currencyGroup" match="payment"
             use="concat(orderCurrency, transactionOperation, transactionGatewayLabel)"/>


    <xsl:template match="/">
        <fo:root font-family="Times" font-size="9">

            <fo:layout-master-set>
                <fo:simple-page-master master-name="A4-landscape"
                                       page-height="21.0cm" page-width="29.7cm" margin="2cm">
                    <fo:region-body margin-top="1cm" margin-bottom="1cm"/>
                    <fo:region-before extent="1cm"/>
                    <fo:region-after extent="1cm"/>
                </fo:simple-page-master>
            </fo:layout-master-set>

            <fo:page-sequence master-reference="A4-landscape">

                <fo:static-content flow-name="xsl-region-before">
                    <fo:block text-align="left" font-weight="bold" font-size="16pt">
                        <fo:inline>
                            <fo:external-graphic  src="report/yes-logo.png" width="35mm" vertical-align="middle"/>
                        </fo:inline>
                        Платижі
                    </fo:block>
                </fo:static-content>

                <fo:static-content flow-name="xsl-region-after">
                    <fo:block text-align="right">
                        стр
                        <fo:page-number/>
                        від
                        <fo:page-number-citation ref-id="end"/>
                    </fo:block>
                </fo:static-content>


                <fo:flow flow-name="xsl-region-body">

                    <xsl:if test="count(./yes-report/payment) > 0">
                        <fo:block border-before-width="2cm" border-width="1mm" space-before="5mm">
                            <fo:table border-before-width="5cm">
                                <fo:table-header>
                                    <fo:table-row font-weight="bold" keep-together="always" keep-with-next="always">
                                        <fo:table-cell border="solid 1px black" width="1cm" text-align="right">
                                            <fo:block font-weight="bold">№</fo:block>
                                        </fo:table-cell>
                                        <fo:table-cell border="solid 1px black" width="2.5cm">
                                            <fo:block font-weight="bold">Замовлення</fo:block>
                                        </fo:table-cell>
                                        <fo:table-cell border="solid 1px black" width="3.0cm">
                                            <fo:block font-weight="bold">Доставка</fo:block>
                                        </fo:table-cell>
                                        <fo:table-cell border="solid 1px black" width="4.5cm">
                                            <fo:block font-weight="bold">Дані карти</fo:block>
                                        </fo:table-cell>
                                        <fo:table-cell border="solid 1px black" width="2cm">
                                            <fo:block font-weight="bold">Операція</fo:block>
                                        </fo:table-cell>

                                        <fo:table-cell border="solid 1px black" width="4.5cm">
                                            <fo:block font-weight="bold">Шлюз / інфо</fo:block>
                                        </fo:table-cell>

                                        <fo:table-cell border="solid 1px black" width="2.5cm">
                                            <fo:block font-weight="bold">Дата</fo:block>
                                        </fo:table-cell>

                                        <fo:table-cell border="solid 1px black" width="2cm">
                                            <fo:block font-weight="bold">Проведений</fo:block>
                                        </fo:table-cell>
                                        <fo:table-cell border="solid 1px black" text-align="right" width="3.0cm">
                                            <fo:block font-weight="bold">Сума</fo:block>
                                        </fo:table-cell>
                                    </fo:table-row>
                                </fo:table-header>

                                <fo:table-body>

                                    <xsl:for-each
                                            select="//payment[generate-id(.)=generate-id(key('currencyGroup', concat(orderCurrency, transactionOperation, transactionGatewayLabel))[1])]">
                                        <xsl:sort select="orderCurrency"/>
                                        <xsl:sort select="transactionOperation"/>
                                        <xsl:sort select="transactionGatewayLabel"/>
                                        <xsl:for-each
                                                select="key('currencyGroup', concat(orderCurrency, transactionOperation, transactionGatewayLabel))">

                                            <fo:table-row>
                                                <fo:table-cell border="solid 1px black" text-align="right">
                                                    <fo:block>
                                                        <xsl:value-of select="position()"/>
                                                    </fo:block>
                                                </fo:table-cell>
                                                <fo:table-cell border="solid 1px black">
                                                    <fo:block>
                                                        <xsl:value-of select="orderNumber"/>
                                                    </fo:block>
                                                </fo:table-cell>
                                                <fo:table-cell border="solid 1px black">
                                                    <fo:block>
                                                        <xsl:value-of select="orderShipment"/>
                                                    </fo:block>
                                                </fo:table-cell>
                                                <fo:table-cell border="solid 1px black">
                                                    <fo:block>
                                                        <xsl:if test="cardNumber">
                                                            <xsl:value-of select="cardHolderName"/>, карта:
                                                            <xsl:value-of select="cardNumber"/> дійсна
                                                            <xsl:value-of select="cardExpireMonth"/>/<xsl:value-of
                                                                select="cardExpireYear"/>
                                                        </xsl:if>
                                                    </fo:block>
                                                </fo:table-cell>
                                                <fo:table-cell border="solid 1px black">
                                                    <fo:block>
                                                        <xsl:value-of select="transactionOperation"/>
                                                    </fo:block>
                                                </fo:table-cell>
                                                <fo:table-cell border="solid 1px black">
                                                    <fo:block font-size="8pt">
                                                        <xsl:value-of select="transactionGatewayLabel"/>
                                                    </fo:block>
                                                    <fo:block font-size="8pt">
                                                        <xsl:value-of select="transactionOperationResultCode"/>
                                                    </fo:block>
                                                    <fo:block font-size="8pt">
                                                        <xsl:value-of select="paymentProcessorResult"/>
                                                    </fo:block>
                                                </fo:table-cell>
                                                <fo:table-cell border="solid 1px black">
                                                    <fo:block>
                                                        <xsl:value-of select="format-dateTime(orderDate, '[D01]/[M01]/[Y0001]')"/>
                                                    </fo:block>
                                                </fo:table-cell>
                                                <fo:table-cell border="solid 1px black">
                                                    <fo:block>
                                                        <xsl:choose>
                                                            <xsl:when test="paymentProcessorBatchSettlement = 'true'">Так</xsl:when>
                                                            <xsl:otherwise>Ні</xsl:otherwise>
                                                        </xsl:choose>
                                                    </fo:block>
                                                </fo:table-cell>
                                                <fo:table-cell border="solid 1px black" text-align="right">
                                                    <fo:block>
                                                        <xsl:value-of select="concat(orderCurrency, ' ')"/>
                                                        <xsl:value-of select="format-number(paymentAmount, '##0.00')"/>
                                                    </fo:block>
                                                </fo:table-cell>

                                            </fo:table-row>


                                            <xsl:if test="position() = last()">
                                                <fo:table-row>
                                                    <fo:table-cell border="solid 1px black"
                                                                   number-columns-spanned="8" font-weight="bold">
                                                        <fo:block>Всього</fo:block>
                                                    </fo:table-cell>
                                                    <fo:table-cell border="solid 1px black" text-align="right"
                                                                   font-weight="bold">
                                                        <fo:block>
                                                            <xsl:value-of
                                                                    select="format-number(sum(
                                                                key('currencyGroup', concat(orderCurrency, transactionOperation, transactionGatewayLabel))/paymentAmount
                                                                ), '##0.00')"/>
                                                        </fo:block>
                                                    </fo:table-cell>
                                                </fo:table-row>
                                                <fo:table-row>
                                                    <fo:table-cell border="none"
                                                                   number-columns-spanned="9" font-weight="bold">
                                                        <fo:block><fo:leader /></fo:block>
                                                    </fo:table-cell>
                                                </fo:table-row>
                                            </xsl:if>


                                        </xsl:for-each>
                                    </xsl:for-each>


                                </fo:table-body>
                            </fo:table>
                        </fo:block>

                        <fo:block>
                        </fo:block>

                    </xsl:if>
                    <fo:block id="end"/>
                </fo:flow>
            </fo:page-sequence>
        </fo:root>
    </xsl:template>
</xsl:stylesheet>