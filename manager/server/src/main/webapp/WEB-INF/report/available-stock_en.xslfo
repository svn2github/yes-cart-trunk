<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="2.0"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:fo="http://www.w3.org/1999/XSL/Format">
    <xsl:output method="xml" encoding="UTF-8" indent="yes"/>
    <xsl:template match="/">
        <fo:root font-family="Times" font-size="9">

            <fo:layout-master-set>
                <fo:simple-page-master master-name="A4-portrait"
                                       page-height="29.7cm" page-width="21.0cm" margin="2cm">
                    <fo:region-body margin-top="1cm" margin-bottom="1cm"/>
                    <fo:region-before extent="1cm"/>
                    <fo:region-after extent="1cm"/>
                </fo:simple-page-master>
            </fo:layout-master-set>

            <fo:page-sequence master-reference="A4-portrait">

                <fo:static-content flow-name="xsl-region-before">
                    <fo:block text-align="left" font-weight="bold" font-size="16pt">
                        <fo:inline>
                            <fo:external-graphic  src="report/yes-logo.png" width="35mm" vertical-align="middle"/>
                        </fo:inline>
                        Inventory
                    </fo:block>
                </fo:static-content>

                <fo:static-content flow-name="xsl-region-after">
                    <fo:block text-align="right">
                        Page <fo:page-number/>  of <fo:page-number-citation ref-id="end"/>
                    </fo:block>
                </fo:static-content>


                <fo:flow flow-name="xsl-region-body">

                    <xsl:if test="count(./yes-report/inventoryDto) > 0">
                        <fo:block border-before-width="2cm" border-width="1mm">

                            <fo:table border-before-width="5cm">
                                <fo:table-header>
                                    <fo:table-row font-weight="bold" keep-together="always" keep-with-next="always">
                                        <fo:table-cell border="solid 1px black" width="1cm" text-align="right">
                                            <fo:block font-weight="bold">#</fo:block>
                                        </fo:table-cell>
                                        <fo:table-cell border="solid 1px black" width="2.5cm">
                                            <fo:block font-weight="bold">Warehouse code</fo:block>
                                        </fo:table-cell>
                                        <fo:table-cell border="solid 1px black" width="3cm">
                                            <fo:block font-weight="bold">Sku Code</fo:block>
                                        </fo:table-cell>
                                        <fo:table-cell border="solid 1px black">
                                            <fo:block font-weight="bold">Name</fo:block>
                                        </fo:table-cell>
                                        <fo:table-cell border="solid 1px black" text-align="right" width="1.5cm">
                                            <fo:block font-weight="bold">Reserved</fo:block>
                                        </fo:table-cell>
                                        <fo:table-cell border="solid 1px black" text-align="right" width="1.5cm">
                                            <fo:block font-weight="bold">Quantity</fo:block>
                                        </fo:table-cell>
                                    </fo:table-row>
                                </fo:table-header>

                                <fo:table-body>
                                    <xsl:for-each select="./yes-report/inventoryDto">
                                        <fo:table-row>
                                            <fo:table-cell border="solid 1px black" text-align="right">
                                                <fo:block>
                                                    <xsl:value-of select="position()"/>
                                                </fo:block>
                                            </fo:table-cell>
                                            <fo:table-cell border="solid 1px black">
                                                <fo:block>
                                                    <xsl:value-of select="warehouseCode"/>
                                                </fo:block>
                                            </fo:table-cell>
                                            <fo:table-cell border="solid 1px black">
                                                <fo:block>
                                                    <xsl:value-of select="skuCode"/>
                                                </fo:block>
                                            </fo:table-cell>
                                            <fo:table-cell border="solid 1px black">
                                                <fo:block>
                                                    <xsl:value-of select="skuName"/>
                                                </fo:block>
                                            </fo:table-cell>
                                            <fo:table-cell border="solid 1px black" text-align="right">
                                                <fo:block>
                                                    <xsl:if test="reserved">
                                                        <xsl:value-of select="format-number(reserved, '#0.00')"/>
                                                    </xsl:if>
                                                </fo:block>
                                            </fo:table-cell>
                                            <fo:table-cell border="solid 1px black" text-align="right">
                                                <fo:block>
                                                    <xsl:value-of select="format-number(quantity, '#0.00')"/>
                                                </fo:block>
                                            </fo:table-cell>
                                        </fo:table-row>
                                    </xsl:for-each>


                                    <fo:table-row>
                                        <fo:table-cell border="solid 1px black" number-columns-spanned="4"
                                                       font-weight="bold">
                                            <fo:block>Summary</fo:block>
                                        </fo:table-cell>
                                        <fo:table-cell border="solid 1px black" text-align="right" font-weight="bold">
                                            <fo:block>
                                                <xsl:value-of
                                                        select="format-number(sum(./yes-report/inventoryDto/reserved), '#0.00')"/>
                                            </fo:block>
                                        </fo:table-cell>
                                        <fo:table-cell border="solid 1px black" text-align="right" font-weight="bold">
                                            <fo:block>
                                                <xsl:value-of
                                                        select="format-number(sum(./yes-report/inventoryDto/quantity), '#0.00')"/>
                                            </fo:block>
                                        </fo:table-cell>
                                    </fo:table-row>

                                    <fo:table-row>
                                        <fo:table-cell border="solid 1px black" number-columns-spanned="5"
                                                       font-weight="bold">
                                            <fo:block>Count</fo:block>
                                        </fo:table-cell>
                                        <fo:table-cell border="solid 1px black" text-align="right" font-weight="bold">
                                            <fo:block>
                                                <xsl:value-of
                                                        select="format-number(count(./yes-report/inventoryDto), '##')"/>
                                            </fo:block>
                                        </fo:table-cell>
                                    </fo:table-row>
                                </fo:table-body>
                            </fo:table>
                        </fo:block>
                    </xsl:if>
                    <fo:block id="end"/>
                </fo:flow>
            </fo:page-sequence>
        </fo:root>
    </xsl:template>
</xsl:stylesheet>