<?xml version="1.0"?>
<!--
  - Copyright 2009 Igor Azarnyi, Denys Pavlov
  -
  -    Licensed under the Apache License, Version 2.0 (the "License");
  -    you may not use this file except in compliance with the License.
  -    You may obtain a copy of the License at
  -
  -        http://www.apache.org/licenses/LICENSE-2.0
  -
  -    Unless required by applicable law or agreed to in writing, software
  -    distributed under the License is distributed on an "AS IS" BASIS,
  -    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  -    See the License for the specific language governing permissions and
  -    limitations under the License.
  -->

<attr:EntityAttributeValuesManagementPanel
           label="@Resource(bundle='ShopManagerApplication',key='attr.values')"
           xmlns:mx="http://www.adobe.com/2006/mxml"
           xmlns:attr="org.yes.cart.ui.attributes.*"
           width="100%"
           height="100%"
           creationComplete="init();">

    <mx:Script><![CDATA[
        import mx.collections.ArrayCollection;
        import mx.events.DataGridEvent;
        import mx.events.ListEvent;
        import mx.rpc.events.ResultEvent;

        import org.yes.cart.constants.AttributeGroupNames;
        import org.yes.cart.impl.AttrValueSystemDTOImpl;
        import org.yes.cart.impl.AttributeDTOImpl;
        import org.yes.cart.shopmanager.ShopManagerGlobal;
        import org.yes.cart.util.DomainUtils;
        import org.yes.cart.util.DomainUtils;

        public function init():void {

            this.attrValues.removeAll();
            onEntitySet(AttributeGroupNames.SYSTEM);

            remoteSystemService.getEntityAttributes(100);
        }

        override protected function onAttributeSelected(event:ListEvent):void {
            canDelete = false;
        }

        override protected function onValueAdded(selectedAttributeDTO:AttributeDTOImpl):void {
            var sysAttr:AttrValueSystemDTOImpl = new AttrValueSystemDTOImpl();
            sysAttr.attributeDTO = selectedAttributeDTO;
            remoteSystemService.createEntityAttributeValue(sysAttr);
        }


        override public function onValueChanged(event:DataGridEvent, attrValue:Object, newValue:Object):void {
            var sysAttr:AttrValueSystemDTOImpl = attrValue as AttrValueSystemDTOImpl;
            sysAttr.val = String(newValue);
            if (DomainUtils.isNewPK(sysAttr.attrvalueId)) {
                remoteSystemService.createEntityAttributeValue(sysAttr);
            } else {
                remoteSystemService.updateEntityAttributeValue(sysAttr);
            }
        }

        override protected function onValueDelete(selectedItem:Object):void {
            /* Not supported for now
             var pk:Number = selectedItem != null ? selectedItem.attrvalueId : 0;
             if (!isNaN(pk) && pk > 0) {
                remoteService.deleteAttributeValue(selectedItem.attrvalueId);
                selectedItem.attrvalueId = Number.NaN;
                selectedItem.val = null;
             }
             */
        }

        /**
         * Overwrite this method to allow different sibling panels to display different
         * groups of attributes
         *
         * @param attr currently loaded (into this panel) attribute
         * @return true to allow this attribute to be managed through this pannel
         */
        protected function allowManageAttribute(attr:AttrValueSystemDTOImpl):Boolean {
            return true; // manage all available attributes by default
        }

        private function onGetEntityAttributesResult(event:ResultEvent):void {
            var attrs:ArrayCollection = event.result as ArrayCollection;
            if (attrs != null) {
                for each (var attr:AttrValueSystemDTOImpl in attrs) {
                    if (allowManageAttribute(attr)) {
                        this.attrValues.addItem(attr);
                    }
                }
            }
        }

        private function onCreateEntityAttributeValueResult(event:ResultEvent):void {
            var sysAttr:AttrValueSystemDTOImpl = event.result as AttrValueSystemDTOImpl;
            var _replace:AttrValueSystemDTOImpl = null;
            for each (var old:AttrValueSystemDTOImpl in this.attrValues) {
                if (DomainUtils.isPersistentPK(old.attrvalueId) && old.attrvalueId == sysAttr.attrvalueId) {
                    _replace = old;
                    break; // this is exact match by id so replace this one
                } else if (old.attributeDTO.attributeId == sysAttr.attributeDTO.attributeId && DomainUtils.isNewPK(old.attrvalueId)) {
                    _replace = old; // this is a candidate - but there can be multi values
                }
            }
            if (_replace != null) {
                _replace.attrvalueId = sysAttr.attrvalueId;
                _replace.val = sysAttr.val;
                _replace.displayVals = sysAttr.displayVals;
            } else {
                this.attrValues.addItem(sysAttr);
                var idx:int = this.attrValues.length - 1;
                if (idx >= 0) {
                    valueDataGrid.selectedIndex = idx;
                    valueDataGrid.validateNow();
                    valueDataGrid.scrollToIndex(idx);
                }
            }
        }

        private function onUpdateEntityAttributeValueResult(event:ResultEvent):void {
            onCreateEntityAttributeValueResult(event);
        }

        private function onDeleteAttributeValueResult(event:ResultEvent):void {
            // ok deleted
        }
        ]]></mx:Script>


    <mx:RemoteObject showBusyCursor="true" id="remoteSystemService"
                     destination="remoteSystemService"
                     channelSet="{ShopManagerGlobal.instance.channelSet}">

        <mx:method id="getEntityAttributes" name="getEntityAttributes"
                   result="onGetEntityAttributesResult(event)"
                   fault="onRpcMethodFault(event)"/>

        <mx:method id="createEntityAttributeValue" name="createEntityAttributeValue"
                   result="onCreateEntityAttributeValueResult(event)"
                   fault="onRpcMethodFault(event)"/>

        <mx:method id="updateEntityAttributeValue" name="updateEntityAttributeValue"
                   result="onUpdateEntityAttributeValueResult(event)"
                   fault="onRpcMethodFault(event)"/>

        <mx:method id="deleteAttributeValue" name="deleteAttributeValue"
                   result="onDeleteAttributeValueResult(event)"
                   fault="onRpcMethodFault(event)"/>

    </mx:RemoteObject>

</attr:EntityAttributeValuesManagementPanel>
