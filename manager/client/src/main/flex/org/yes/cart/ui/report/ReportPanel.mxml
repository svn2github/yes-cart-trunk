<?xml version="1.0" encoding="utf-8"?>
<!--
  - Copyright 2009 Igor Azarnyi, Denys Pavlov
  -
  -    Licensed under the Apache License, Version 2.0 (the "License");
  -    you may not use this file except in compliance with the License.
  -    You may obtain a copy of the License at
  -
  -        http://www.apache.org/licenses/LICENSE-2.0
  -
  -    Unless required by applicable law or agreed to in writing, software
  -    distributed under the License is distributed on an "AS IS" BASIS,
  -    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  -    See the License for the specific language governing permissions and
  -    limitations under the License.
  -->

<mx:Panel xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:core="mx.core.*" width="100%" height="100%"
          title="@Resource(bundle='ReportPanel',key='reports')"
          paddingLeft="2" paddingTop="2" paddingRight="2" paddingBottom="2"
          creationComplete="init();">

    <mx:Script>
        <![CDATA[
        import com.georg.DateTimeField;
        import com.hexagonstar.util.debug.Debug;

        import mx.collections.ArrayList;
        import mx.containers.FormItem;

        import mx.controls.Alert;
        import mx.controls.DateField;
        import mx.controls.TextInput;
        import mx.core.UIComponent;
        import mx.rpc.AsyncResponder;
        import mx.rpc.events.FaultEvent;
        import mx.rpc.events.ResultEvent;

        import org.yes.cart.report.impl.ReportDescriptor;
        import org.yes.cart.report.impl.ReportPair;
        import org.yes.cart.report.impl.ReportParameter;
        import org.yes.cart.shopmanager.ShopManagerGlobal;

        [Bindable]
        private var pdf:ByteArray;

        private var reportName:String;

        private function init():void {
            localeFilter = resourceManager.getString('ReportPanel', 'localeFilter');
            remoteReportService.getReportDescriptors();

        }


        /**
         * Get report display name, which depends from current locale.
         * @param item report descriptor.
         * @return report display name.
         */
        private function getReportLabel(itm:Object):String {
            var reportDesriptor:ReportDescriptor = itm as ReportDescriptor;
            var rez:String = reportDesriptor.reportId;
            try {
                for (var i:int = 0; i < reportDesriptor.langLabel.length; i++) {
                    var langLabel:ReportPair = ReportPair(reportDesriptor.langLabel.getItemAt(i));
                    if (langLabel != null) {
                        Debug.trace(langLabel);
                        if (langLabel.lang == localeFilter) {
                            rez = langLabel.value;
                            break;
                        }

                    }
                }

            } catch (errObject:Error) {
                rez = rez + errObject.message;
            }


            return rez;
        }

        /**
         * Fill parameter form with new parameters to get user input for selected report.
         * @param event
         */
        private function fillParameterForm(event:Event):void {


            var reportDescriptor:ReportDescriptor = ReportDescriptor(reportComboBox.selectedItem);

            parameterForm.removeAllChildren();

            pdf = null;

            Debug.trace("Report has " + reportDescriptor.parameters.length + " parameters");

            try {


                var debugStr:String = "";

                for (var i:int = 0; i < reportDescriptor.parameters.length; i++) {

                    var repoParam:ReportParameter = ReportParameter(reportDescriptor.parameters.getItemAt(i));
                    debugStr += repoParam + "\n";
                    for (var j:int = 0; j < repoParam.langLabel.length; j++) { //loop through languages


                        var localizedParamName:ReportPair = ReportPair(repoParam.langLabel.getItemAt(j));

                        debugStr += "       " + localizedParamName + "\n";

                        if (localizedParamName.lang == localeFilter) {

                            var formItem:FormItem = new FormItem();
                            formItem.label = localizedParamName.value;

                            if (repoParam.allowedValuesQuery != null && repoParam.allowedValuesQuery.length > 0) {
                                var cbmBox:ComboBox = new ComboBox();
                                cbmBox.prompt = resourceManager.getString('ReportPanel', 'selectDataPrompt');
                                cbmBox.labelField = "value";
                                remoteReportService.getParameterValues(repoParam.allowedValuesQuery).addResponder(
                                        new AsyncResponder(
                                                function (event:ResultEvent, obj:Object = null):void {
                                                    cbmBox.dataProvider = event.result;
                                                }
                                                ,
                                                function (event:FaultEvent):void {
                                                    ShopManagerGlobal.instance.defaultOnRpcMethodFault(event);
                                                }
                                        )
                                );
                                formItem.addChild(cbmBox); //todo data

                            } else if (repoParam.businesstype == "Date") {
                                //var offsetDay = 1000*60*60*24;// milliseconds in one day
                                var dtf:DateTimeField = new DateTimeField();
                                //dtf.selectedDate = new Date( (new Date()).getTime() - offsetDay ); //one day ago
                                formItem.addChild(dtf);
                            } else {
                                formItem.addChild(new TextInput());
                            }

                            parameterForm.addChild(formItem);

                        }
                    }
                }

                Debug.trace(debugStr);
            } catch (e:Error) {
                Debug.trace("" + e);
            }

            okBtnCrtlBar.width = parameterForm.width;
        }


        /**
         * Collect report parameters from form.
         * @returnarray list , which contains report parameters
         */
        private function collectReportParameters():ArrayList {
            var rez:ArrayList = new ArrayList();
            for (var i:int = 0; i < parameterForm.getChildren().length; i++) {
                var item:Object = parameterForm.getChildAt(i);
                if (item is FormItem) {
                    var formItem:FormItem = item as FormItem;
                    var uiComponent:UIComponent = UIComponent(formItem.getChildAt(0));
                    if (uiComponent is ComboBox) {
                        rez.addItem(ComboBox(uiComponent).selectedItem.lang);
                    } else if (uiComponent is DateTimeField) {
                        rez.addItem(DateTimeField(uiComponent).selectedDate);
                    } else if (uiComponent is TextInput) {
                        rez.addItem(TextInput(uiComponent).text);
                    } else {
                        Debug.trace(" Cant determinate ui type for  " + formItem + " value extracted as null and will be passed as report parameter");
                        rez.addItem(null);
                    }
                }
            }
            return rez;

        }

        private function onDownloadReportClick(event:MouseEvent):void {
            //http://forums.adobe.com/message/3614898
            var fileReference:FileReference = new FileReference();

            fileReference.save(
                    pdf,
                    reportName
            );

        }

        /**
         * Create report on server side. Donload as byte array and prompt user to save it on disk.
         *
         * @param event mouse event
         */
        private function createReport(event:MouseEvent):void {

            pdf = null;
            reportName = getReportLabel(reportComboBox.selectedItem) + "_" + DateField.dateToString(new Date(), "DD-MM-YYYY") + ".pdf";

            for (var obj:Object in parameterForm.getChildren()) {
                Debug.traceObj(obj, 10);
            }

            var reportDescriptor:ReportDescriptor = ReportDescriptor(reportComboBox.selectedItem);

            remoteReportService.downloadReport(
                    localeFilter,
                    reportDescriptor.reportId,
                    collectReportParameters()
            );

        }

        private function onRpcMethodFault(event:FaultEvent):void {
            ShopManagerGlobal.instance.defaultOnRpcMethodFault(event);
        }

        private function onDownloadReportResult(event:ResultEvent):void {
            pdf = ByteArray(event.result);
            Alert.show(
                    resourceManager.getString("ReportPanel", "reportReadyToSave"),
                    resourceManager.getString("ReportPanel", "info"));
        }
        ]]>
    </mx:Script>

    <mx:String id="localeFilter"/>


    <mx:RemoteObject showBusyCursor="true" id="remoteReportService"
                     destination="remoteReportService"
                     channelSet="{ShopManagerGlobal.instance.channelSet}">
        <mx:makeObjectsBindable>true</mx:makeObjectsBindable>

        <mx:method id="downloadReport" name="downloadReport"
                   result="onDownloadReportResult(event)"
                   fault="onRpcMethodFault(event)"/>

    </mx:RemoteObject>

    <mx:Canvas width="100%" height="100%">
        <mx:VBox x="0" y="0" width="100%" height="100%" id="shopManagmenlPanel">
            <mx:ControlBar width="100%">
                <mx:Text text="@Resource(bundle='ReportPanel',key='report')"/>
                <mx:ComboBox id="reportComboBox"
                             labelFunction="getReportLabel"
                             data="reportId"
                             dataProvider="{remoteReportService.getReportDescriptors.lastResult}"
                             change="fillParameterForm(event)"
                             prompt="@Resource(bundle='ReportPanel',key='selectReportPropmt')">
                </mx:ComboBox>
            </mx:ControlBar>

            <mx:Form id="parameterForm" visible="{reportComboBox.selectedItem != null}" x="0" y="0" borderStyle="solid"
                     borderVisible="true">
                <mx:FormItem label="@Resource(bundle='BrandManagementPanel',key='name')">
                    <mx:TextInput id="nameTextImput" width="200"/>
                </mx:FormItem>
                <mx:FormItem label="@Resource(bundle='BrandManagementPanel',key='description')">
                    <mx:TextArea id="descriptionTextInput" width="200"/>
                </mx:FormItem>


            </mx:Form>

            <mx:ControlBar id="okBtnCrtlBar"
                           width="100%" visible="{reportComboBox.selectedItem != null}">


                <mx:Button visible="{pdf != null}" click="onDownloadReportClick(event)"
                           id="saveBtn" label="@Resource(bundle='ReportPanel',key='save')"/>

                <mx:Button visible="{reportComboBox.selectedItem != null}" click="createReport(event)"
                           id="okBtn" label="@Resource(bundle='ReportPanel',key='ok')"/>

            </mx:ControlBar>


        </mx:VBox>
    </mx:Canvas>


</mx:Panel>