<?xml version="1.0" encoding="utf-8"?>
<!--
  - Copyright 2009 Igor Azarnyi, Denys Pavlov
  -
  -    Licensed under the Apache License, Version 2.0 (the "License");
  -    you may not use this file except in compliance with the License.
  -    You may obtain a copy of the License at
  -
  -        http://www.apache.org/licenses/LICENSE-2.0
  -
  -    Unless required by applicable law or agreed to in writing, software
  -    distributed under the License is distributed on an "AS IS" BASIS,
  -    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  -    See the License for the specific language governing permissions and
  -    limitations under the License.
  -->

<mx:Panel xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:core="mx.core.*" width="100%" height="100%"
          title="@Resource(bundle='ReportPanel',key='reports')"
          paddingLeft="2" paddingTop="2" paddingRight="2" paddingBottom="2"
          creationComplete="init();">

    <mx:Script>
        <![CDATA[
        import com.georg.DateTimeField;
        import com.hexagonstar.util.debug.Debug;

        import mx.collections.ArrayList;
        import mx.containers.FormItem;

        import mx.controls.Alert;
        import mx.controls.DateField;
        import mx.controls.TextInput;
        import mx.core.UIComponent;
        import mx.events.CloseEvent;
        import mx.events.ValidationResultEvent;
        import mx.rpc.AsyncResponder;
        import mx.rpc.events.FaultEvent;
        import mx.rpc.events.ResultEvent;
        import mx.validators.DateValidator;
        import mx.validators.NumberValidator;
        import mx.validators.StringValidator;
        import mx.validators.Validator;

        import org.yes.cart.report.impl.ReportDescriptor;
        import org.yes.cart.report.impl.ReportPair;
        import org.yes.cart.report.impl.ReportParameter;
        import org.yes.cart.shopmanager.ShopManagerGlobal;

        [Bindable]
        private var pdf:ByteArray;
        private var validationArray:Array = new Array();

        private var reportName:String;

        private function init():void {
            localeFilter = resourceManager.getString('Common', 'localeFilter');
            remoteReportService.getReportDescriptors();
        }


        /**
         * Get report display name, which depends from current locale.
         * @param item report descriptor.
         * @return report display name.
         */
        private function getReportLabel(itm:Object):String {
            var reportDesriptor:ReportDescriptor = itm as ReportDescriptor;
            var rez:String = reportDesriptor.reportId;
            for (var i:int = 0; i < reportDesriptor.langLabel.length; i++) {
                var langLabel:ReportPair = ReportPair(reportDesriptor.langLabel.getItemAt(i));
                if (langLabel != null) {
                    Debug.trace(langLabel);
                    if (langLabel.lang == localeFilter) {
                        rez = langLabel.value;
                        break;
                    }
                }
            }
            return rez;
        }

        /**
         * Fill parameter form with new parameters to get user input for selected report.
         * @param event
         */
        private function onReportComboboxChange(event:Event):void {
            var reportDescriptor:ReportDescriptor = ReportDescriptor(reportComboBox.selectedItem);
            parameterForm.removeAllChildren();
            validationArray = new Array();
            pdf = null;
            Debug.trace("DEBUG Report has " + reportDescriptor.parameters.length + " parameters");
            try {
                for (var i:int = 0; i < reportDescriptor.parameters.length; i++) {
                    var repoParam:ReportParameter = ReportParameter(reportDescriptor.parameters.getItemAt(i));
                    for (var j:int = 0; j < repoParam.langLabel.length; j++) { //loop through languages
                        var localizedParamName:ReportPair = ReportPair(repoParam.langLabel.getItemAt(j));
                        if (localizedParamName.lang == localeFilter) {
                            var formItem:FormItem = new FormItem();
                            formItem.label = localizedParamName.value;
                            if (repoParam.allowedValuesQuery != null && repoParam.allowedValuesQuery.length > 0) {
                                var cbmBox:ComboBox = new ComboBox();
                                cbmBox.prompt = resourceManager.getString('ReportPanel', 'selectDataPrompt');
                                cbmBox.labelField = "value";
                                remoteReportService.getParameterValues(repoParam.allowedValuesQuery).addResponder(
                                        new AsyncResponder(
                                                function (event:ResultEvent, obj:Object = null):void {
                                                    cbmBox.dataProvider = event.result;
                                                }
                                                ,
                                                function (event:FaultEvent):void {
                                                    ShopManagerGlobal.instance.defaultOnRpcMethodFault(event);
                                                }
                                        )
                                );
                                if (repoParam.mandatory) {
                                    var numberValidator:NumberValidator = new NumberValidator();
                                    numberValidator.source = cbmBox;
                                    numberValidator.property = "selectedIndex";
                                    numberValidator.minValue = 0; // -1 means no selection
                                    numberValidator.lowerThanMinError = resourceManager.getString('ReportPanel', 'cmbSelectValue');
                                    validationArray.push(numberValidator);
                                }
                                formItem.addChild(cbmBox);
                            } else if (repoParam.businesstype == "Date") {
                                var dtf:DateTimeField = new DateTimeField();
                                if (repoParam.mandatory) {
                                    var dataValidator:DateValidator = new DateValidator();
                                    dataValidator.source = dtf;
                                    dataValidator.property =  "selectedDate";
                                    dataValidator.required = true;
                                    validationArray.push(dataValidator);
                                }
                                formItem.addChild(dtf);
                            } else {
                                var textInput:TextInput = new TextInput();
                                if (repoParam.mandatory) {
                                    var stringValidator:StringValidator = new StringValidator();
                                    stringValidator.source = textInput;
                                    stringValidator.property = "text";
                                    stringValidator.required = true;
                                    stringValidator.maxLength = 1;
                                    validationArray.push(stringValidator);
                                }
                                formItem.addChild(textInput);
                            }

                            parameterForm.addChild(formItem);

                        }
                    }
                }
            } catch (e:Error) {
                Debug.trace("DEBUG error " + e);
            }

        }


        /**
         * Collect report parameters from form.
         * @returnarray list , which contains report parameters
         */
        private function collectReportParameters():ArrayList {
            var rez:ArrayList = new ArrayList();
            for (var i:int = 0; i < parameterForm.getChildren().length; i++) {
                var item:Object = parameterForm.getChildAt(i);
                if (item is FormItem) {
                    var formItem:FormItem = item as FormItem;
                    var uiComponent:UIComponent = UIComponent(formItem.getChildAt(0));
                    if (uiComponent is ComboBox) {
                        rez.addItem(ComboBox(uiComponent).selectedItem.lang);
                    } else if (uiComponent is DateTimeField) {
                        rez.addItem(DateTimeField(uiComponent).selectedDate);
                    } else if (uiComponent is TextInput) {
                        rez.addItem(TextInput(uiComponent).text);
                    } else {
                        Debug.trace(" Cant determinate ui type for  " + formItem + " value extracted as null and will be passed as report parameter");
                        rez.addItem(null);
                    }
                }
            }
            return rez;

        }

        /**
         * Save report to disk.
         * @param event  mouse event
         */
        private function onDownloadReportClick(event:CloseEvent):void {
            if (event.detail == Alert.YES) { //save
                //http://forums.adobe.com/message/3614898
                new FileReference().save(
                        pdf,
                        reportName
                );
                pdf = null;
            }
        }

        /**
         * Create report on server side. Donload as byte array and prompt user to save it on disk.
         *
         * @param event mouse event
         */
        private function onCreateReportButtonClick(event:MouseEvent):void {
            var errArr:Array = Validator.validateAll(validationArray);
            if(errArr.length == 0) {
                pdf = null;
                reportName = getReportLabel(reportComboBox.selectedItem) + "_" + DateField.dateToString(new Date(), "DD-MM-YYYY") + ".pdf";
                var reportDescriptor:ReportDescriptor = ReportDescriptor(reportComboBox.selectedItem);
                remoteReportService.downloadReport(
                        localeFilter,
                        reportDescriptor.reportId,
                        collectReportParameters()
                );
            } else {
                var err:ValidationResultEvent;
                var errorMessageArray:Array = [];
                for each (err in errArr) {
                    var errField:String = FormItem(err.currentTarget.source.parent).label;
                    errorMessageArray.push(errField + " : " + err.message);
                }
                Alert.show(
                        errorMessageArray.join("\n\n"),
                        resourceManager.getString('ReportPanel', 'invalidForm'),
                        Alert.OK);
            }
        }

        private function onRpcMethodFault(event:FaultEvent):void {
            ShopManagerGlobal.instance.defaultOnRpcMethodFault(event);
        }

        private function onDownloadReportResult(event:ResultEvent):void {
            pdf = ByteArray(event.result);
            Alert.show(
                    resourceManager.getString("ReportPanel", "reportReadyToSave"),
                    resourceManager.getString("ReportPanel", "info"),
                    Alert.YES | Alert.NO, null, onDownloadReportClick, null, Alert.NO);
        }
        ]]>
    </mx:Script>

    <mx:String id="localeFilter"/>


    <mx:RemoteObject showBusyCursor="true" id="remoteReportService"
                     destination="remoteReportService"
                     channelSet="{ShopManagerGlobal.instance.channelSet}">
        <mx:makeObjectsBindable>true</mx:makeObjectsBindable>

        <mx:method id="downloadReport" name="downloadReport"
                   result="onDownloadReportResult(event)"
                   fault="onRpcMethodFault(event)"/>

    </mx:RemoteObject>

    <mx:Canvas width="100%" height="100%">
        <mx:VBox x="0" y="0" width="100%" height="100%" id="shopManagmenlPanel">
            <mx:ControlBar width="100%">

                <mx:Text text="@Resource(bundle='ReportPanel',key='report')"/>

                <mx:ComboBox id="reportComboBox"
                             labelFunction="getReportLabel"
                             data="reportId"
                             dataProvider="{remoteReportService.getReportDescriptors.lastResult}"
                             change="onReportComboboxChange(event)"
                             prompt="@Resource(bundle='ReportPanel',key='selectReportPropmt')">
                </mx:ComboBox>

                <mx:Button visible="{reportComboBox.selectedItem != null}" click="onCreateReportButtonClick(event)"
                           id="okBtn" label="@Resource(bundle='ReportPanel',key='ok')"/>

            </mx:ControlBar>

            <mx:Form id="parameterForm" visible="{reportComboBox.selectedItem != null}" x="0" y="0">
                <mx:FormItem label="@Resource(bundle='BrandManagementPanel',key='name')">
                    <mx:TextInput id="nameTextImput" width="200"/>
                </mx:FormItem>
                <mx:FormItem label="@Resource(bundle='BrandManagementPanel',key='description')">
                    <mx:TextArea id="descriptionTextInput" width="200"/>
                </mx:FormItem>


            </mx:Form>


        </mx:VBox>
    </mx:Canvas>


</mx:Panel>