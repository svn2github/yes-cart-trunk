<?xml version="1.0"?>
<!--
  - Copyright 2009 Igor Azarnyi, Denys Pavlov
  -
  -    Licensed under the Apache License, Version 2.0 (the "License");
  -    you may not use this file except in compliance with the License.
  -    You may obtain a copy of the License at
  -
  -        http://www.apache.org/licenses/LICENSE-2.0
  -
  -    Unless required by applicable law or agreed to in writing, software
  -    distributed under the License is distributed on an "AS IS" BASIS,
  -    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  -    See the License for the specific language governing permissions and
  -    limitations under the License.
  -->

<attr:EntityAttributeValuesManagementPanel
           label="@Resource(bundle='CustomerManagementPanel',key='customerSummary')"
           xmlns:mx="http://www.adobe.com/2006/mxml"
           xmlns:attr="org.yes.cart.ui.attributes.*"
           width="100%"
           height="100%">

    <mx:Script><![CDATA[
        import mx.collections.ArrayCollection;
        import mx.events.DataGridEvent;
        import mx.rpc.events.ResultEvent;

        import org.yes.cart.constants.AttributeGroupNames;
        import org.yes.cart.impl.AttrValueCustomerDTOImpl;
        import org.yes.cart.impl.AttributeDTOImpl;
        import org.yes.cart.impl.CustomerDTOImpl;
        import org.yes.cart.shopmanager.ShopManagerGlobal;
        import org.yes.cart.util.DomainUtils;

        private var _customer:CustomerDTOImpl;

        public function get customer():CustomerDTOImpl {
            return _customer;
        }

        public function set customer(value:CustomerDTOImpl):void {
            if (_customer != null && value != null && _customer.customerId == value.customerId) {
                return;
            }
            _customer = value;

            this.attrValues.removeAll();
            onEntitySet(AttributeGroupNames.CUSTOMER);

            if (_customer != null) {
                remoteCustomerService.getEntityAttributes(_customer.customerId);
            }
        }

        override protected function onValueAdded(selectedAttributeDTO:AttributeDTOImpl):void {
            var customerAttr:AttrValueCustomerDTOImpl = new AttrValueCustomerDTOImpl();
            customerAttr.attributeDTO = selectedAttributeDTO;
            customerAttr.customerId = this._customer.customerId;
            remoteCustomerService.createEntityAttributeValue(customerAttr);
        }


        override protected function onValueChanged(event:DataGridEvent, attrValue:Object, newValue:Object):void {
            var brandAttr:AttrValueCustomerDTOImpl = attrValue as AttrValueCustomerDTOImpl;
            brandAttr.val = String(newValue);
            if (DomainUtils.isNewPK(brandAttr.attrvalueId)) {
                remoteCustomerService.createEntityAttributeValue(brandAttr);
            } else {
                remoteCustomerService.updateEntityAttributeValue(brandAttr);
            }
        }

        override protected function onValueDelete(selectedItem:Object):void {
            this.attrValues.removeItemAt(this.attrValues.getItemIndex(selectedItem));
            remoteCustomerService.deleteAttributeValue(selectedItem.attrvalueId);
        }

        private function onGetEntityAttributesResult(event:ResultEvent):void {
            var attrs:ArrayCollection = event.result as ArrayCollection;
            if (attrs != null) {
                for each (var attr:AttrValueCustomerDTOImpl in attrs) {
                    if (attr.attributeDTO.code.indexOf("IMAGE") == -1) {
                        this.attrValues.addItem(attr);
                    }
                }
            }
        }

        private function onCreateEntityAttributeValueResult(event:ResultEvent):void {
            var brandAttr:AttrValueCustomerDTOImpl = event.result as AttrValueCustomerDTOImpl;
            var index:int = -1;
            for each (var old:AttrValueCustomerDTOImpl in this.attrValues) {
                index++;
                if (old.attributeDTO.attributeId == brandAttr.attributeDTO.attributeId) {
                    break;
                }
            }
            if (index != -1) {
                this.attrValues.removeItemAt(index);
                this.attrValues.addItemAt(brandAttr, index);
            } else {
                this.attrValues.addItem(brandAttr);
            }
        }

        private function onUpdateEntityAttributeValueResult(event:ResultEvent):void {
            var brandAttr:AttrValueCustomerDTOImpl = event.result as AttrValueCustomerDTOImpl;
            var index:int = -1;
            for each (var old:AttrValueCustomerDTOImpl in this.attrValues) {
                index++;
                if (old.attrvalueId == brandAttr.attrvalueId) {
                    break;
                }
            }
            if (index != -1) {
                this.attrValues.removeItemAt(index);
                this.attrValues.addItemAt(brandAttr, index);
            }
        }

        private function onDeleteAttributeValueResult(event:ResultEvent):void {
            // ok deleted
        }
        ]]></mx:Script>


    <mx:RemoteObject showBusyCursor="true" id="remoteCustomerService"
                     destination="remoteCustomerService"
                     channelSet="{ShopManagerGlobal.instance.channelSet}">

        <mx:method id="getEntityAttributes" name="getEntityAttributes"
                   result="onGetEntityAttributesResult(event)"
                   fault="onRpcMethodFault(event)"/>

        <mx:method id="createEntityAttributeValue" name="createEntityAttributeValue"
                   result="onCreateEntityAttributeValueResult(event)"
                   fault="onRpcMethodFault(event)"/>

        <mx:method id="updateEntityAttributeValue" name="updateEntityAttributeValue"
                   result="onUpdateEntityAttributeValueResult(event)"
                   fault="onRpcMethodFault(event)"/>

        <mx:method id="deleteAttributeValue" name="deleteAttributeValue"
                   result="onDeleteAttributeValueResult(event)"
                   fault="onRpcMethodFault(event)"/>

    </mx:RemoteObject>

</attr:EntityAttributeValuesManagementPanel>
