<?xml version="1.0"?>
<!--
  - Copyright 2009 Igor Azarnyi, Denys Pavlov
  -
  -    Licensed under the Apache License, Version 2.0 (the "License");
  -    you may not use this file except in compliance with the License.
  -    You may obtain a copy of the License at
  -
  -        http://www.apache.org/licenses/LICENSE-2.0
  -
  -    Unless required by applicable law or agreed to in writing, software
  -    distributed under the License is distributed on an "AS IS" BASIS,
  -    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  -    See the License for the specific language governing permissions and
  -    limitations under the License.
  -->

<attr:EntityAttributeValuesManagementPanel
           label="@Resource(bundle='BrandManagementPanel',key='images')"
           xmlns:mx="http://www.adobe.com/2006/mxml"
           xmlns:attr="org.yes.cart.ui.attributes.*"
           width="100%"
           height="100%">

    <mx:Script><![CDATA[
        import mx.collections.ArrayCollection;
        import mx.events.DataGridEvent;
        import mx.rpc.events.ResultEvent;

        import org.yes.cart.constants.AttributeGroupNames;
        import org.yes.cart.impl.AttrValueBrandDTOImpl;
        import org.yes.cart.impl.AttributeDTOImpl;
        import org.yes.cart.impl.BrandDTOImpl;
        import org.yes.cart.shopmanager.ShopManagerGlobal;
        import org.yes.cart.util.DomainUtils;

        private var _brand:BrandDTOImpl;

        public function get brand():BrandDTOImpl {
            return _brand;
        }

        public function set brand(value:BrandDTOImpl):void {
            if (_brand != null && value != null && _brand.brandId == value.brandId) {
                return;
            }
            _brand = value;

            this.attrValues.removeAll();
            this.attrValues.refresh(); // this helps with index exception in removeAll caused by grid component sorting
            onEntitySet(AttributeGroupNames.BRAND);

            if (_brand != null) {
                remoteBrandService.getEntityAttributes(_brand.brandId);
            }
        }

        override protected function onValueAdded(selectedAttributeDTO:AttributeDTOImpl):void {
            var brandAttr:AttrValueBrandDTOImpl = new AttrValueBrandDTOImpl();
            brandAttr.attributeDTO = selectedAttributeDTO;
            brandAttr.brandId = this._brand.brandId;
            remoteBrandService.createEntityAttributeValue(brandAttr);
        }


        override public function onValueChanged(event:DataGridEvent, attrValue:Object, newValue:Object):void {
            var brandAttr:AttrValueBrandDTOImpl = attrValue as AttrValueBrandDTOImpl;
            brandAttr.val = String(newValue);
            if (DomainUtils.isNewPK(brandAttr.attrvalueId)) {
                remoteBrandService.createEntityAttributeValue(brandAttr);
            } else {
                remoteBrandService.updateEntityAttributeValue(brandAttr);
            }
        }

        override protected function onValueDelete(selectedItem:Object):void {
            var pk:Number = selectedItem != null ? selectedItem.attrvalueId : 0;
            if (!isNaN(pk) && pk > 0) {
                remoteBrandService.deleteAttributeValue(selectedItem.attrvalueId);
                selectedItem.attrvalueId = Number.NaN;
                selectedItem.val = null;
            }
        }

        /**
         * Overwrite this method to allow different sibling panels to display different
         * groups of attributes
         *
         * @param attr currently loaded (into this panel) attribute
         * @return true to allow this attribute to be managed through this panel
         */
        protected function allowManageAttribute(attr:AttrValueBrandDTOImpl):Boolean {
            return true; // manage all available attributes by default
        }

        private function onGetEntityAttributesResult(event:ResultEvent):void {
            var attrs:ArrayCollection = event.result as ArrayCollection;
            if (attrs != null) {
                for each (var attr:AttrValueBrandDTOImpl in attrs) {
                    if (allowManageAttribute(attr)) {
                        this.attrValues.addItem(attr);
                    }
                }
            }
        }

        private function onCreateEntityAttributeValueResult(event:ResultEvent):void {
            var brandAttr:AttrValueBrandDTOImpl = event.result as AttrValueBrandDTOImpl;
            var _replace:AttrValueBrandDTOImpl = null;
            for each (var old:AttrValueBrandDTOImpl in this.attrValues) {
                if (DomainUtils.isPersistentPK(old.attrvalueId) && old.attrvalueId == brandAttr.attrvalueId) {
                    _replace = old;
                    break; // this is exact match by id so replace this one
                } else if (old.attributeDTO.attributeId == brandAttr.attributeDTO.attributeId && DomainUtils.isNewPK(old.attrvalueId)) {
                    _replace = old; // this is a candidate - but there can be multi values
                }
            }
            if (_replace != null) {
                _replace.attrvalueId = brandAttr.attrvalueId;
                _replace.val = brandAttr.val;
                _replace.displayVals = brandAttr.displayVals;
            } else {
                this.attrValues.addItem(brandAttr);
                var idx:int = this.attrValues.length - 1;
                if (idx >= 0) {
                    valueDataGrid.selectedIndex = idx;
                    valueDataGrid.validateNow();
                    valueDataGrid.scrollToIndex(idx);
                }
            }
        }

        private function onUpdateEntityAttributeValueResult(event:ResultEvent):void {
            onCreateEntityAttributeValueResult(event);
        }

        private function onDeleteAttributeValueResult(event:ResultEvent):void {
            // ok deleted
        }
        ]]></mx:Script>


    <mx:RemoteObject showBusyCursor="true" id="remoteBrandService"
                     destination="remoteBrandService"
                     channelSet="{ShopManagerGlobal.instance.channelSet}">

        <mx:method id="getEntityAttributes" name="getEntityAttributes"
                   result="onGetEntityAttributesResult(event)"
                   fault="onRpcMethodFault(event)"/>

        <mx:method id="createEntityAttributeValue" name="createEntityAttributeValue"
                   result="onCreateEntityAttributeValueResult(event)"
                   fault="onRpcMethodFault(event)"/>

        <mx:method id="updateEntityAttributeValue" name="updateEntityAttributeValue"
                   result="onUpdateEntityAttributeValueResult(event)"
                   fault="onRpcMethodFault(event)"/>

        <mx:method id="deleteAttributeValue" name="deleteAttributeValue"
                   result="onDeleteAttributeValueResult(event)"
                   fault="onRpcMethodFault(event)"/>

    </mx:RemoteObject>

</attr:EntityAttributeValuesManagementPanel>
